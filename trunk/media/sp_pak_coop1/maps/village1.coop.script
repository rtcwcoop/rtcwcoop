//
//Brush Scripting
//Map Village1
//Last edited by: Mike (110501)
//

game_manager
{
	spawn
	{
		accum 0 set 0 // objectives
		accum 1 set 0

		wait 50	//Prevent bizzare things
		trigger player map_start	//Prevents late players affecting the game
	}

	trigger init
	{
		accum 1 abort_if_not_equal 0
		accum 1 set 1

		mu_start sound/music/m_stealth
	}
	
	trigger checkexit
	{
		accum 0 abort_if_not_bitset 1
		accum 0 abort_if_not_bitset 2
		trigger player exitlevel
	}
	
	trigger objective1
	{
		accum 0 bitset 1
	}
	
	trigger objective2
	{
		accum 0 bitset 2
	}
}

sign1
{
	spawn
	{
		wait 5
		trigger sign1 swingloop
	}

	trigger swingloop
	{
		faceangles 2 0 0 125
		faceangles 4 0 0 150
		faceangles 6 0 0 175
		faceangles 8 0 0 200
		faceangles 10 0 0 250
		faceangles 8 0 0 200
		faceangles 6 0 0 175
		faceangles 4 0 0 150
		faceangles 2 0 0 125
		faceangles 0 0 0 100
		faceangles -2 0 0 125
		faceangles -4 0 0 150
		faceangles -6 0 0 175
		faceangles -8 0 0 200
		faceangles -10 0 0 250
		faceangles -8 0 0 200
		faceangles -6 0 0 175
		faceangles -4 0 0 150
		faceangles -2 0 0 125
		faceangles 0 0 0 100
		trigger sign1 swingloop
	}

	death
	{
		alertentity sign1debris
	}
}

bookshelf
{
	spawn
	{
		accum 0 bitreset 1	// Used for Partisan4 to check the player.
		accum 1 set 0		// Used to count dead guys for Partisan4
		accum 2 set 0		// Used for partisan4's response.
	}

	// --- Sync all cinematic1 ---

	trigger cinematic1
	{
		wait 1000
		trigger player cam1
		wait 4000
		trigger nazi0 talk2
		wait 4000
		trigger nazi1 talk2
		trigger nazi0b go
		wait 6000
		trigger nazi0 talk3
		wait 5000
		trigger nazi1 talk3
		trigger nazi0a go
		wait 2000
		wait 500
		trigger nazi0a whistle
		wait 5600
		trigger nazi0a whistle
		wait 3000
		wait 100
		trigger cine1_kessler go
		trigger cine1_bj go
		wait 150
		trigger player cam1d
		wait 12000
		trigger bookshelf cleanup		
	}

	trigger cleanup
	{
		resetscript
		trigger nazi0 reset
		trigger nazi1 reset
		alertentity kill_cine1
		trigger kessler go
	}

	// --- End Sync all cinematic1 ---

	trigger open
	{
		playsound sound/world/winerack.wav loop
		gotomarker bookspot2 30
		stopsound
		wait 3000
		trigger slidefloor open
	}


	trigger count2 	// Since there is no more counter2, I am using the bookshelf to do all the work.
	{
		accum 1 inc 1
		accum 1 abort_if_less_than 4
		alertentity partisan4
		resetscript		
	}

	trigger n1check
	{
		accum 4 abort_if_less_than 1
		trigger nazi5 turntalk
	}

	trigger set2to1
	{
		accum 2 set 1
	}

	trigger p4saystuff
	{
		accum 2 abort_if_less_than 1
		accum 2 abort_if_greater_than 5
		accum 2 inc 1
		trigger bookshelf p4say1
		wait 5
		trigger bookshelf p4say2
		wait 5
		trigger bookshelf p4say3
		wait 5
		trigger bookshelf p4say4
		resetscript
	}

	trigger p4say1
	{
		accum 2 abort_if_not_equal 2
		resetscript
		trigger partisan4 say1
		accum 0 set 2
	}

	trigger p4say2
	{
		accum 2 abort_if_not_equal 3
		trigger partisan4 say2	
		wait 200
		accum 0 set 3
	}

	trigger p4say3
	{
		accum 2 abort_if_not_equal 4
		trigger partisan4 say3		
		wait 200
		accum 0 set 4
	}

	trigger p4say4
	{
		accum 2 abort_if_not_equal 5
		trigger partisan4 say4		
		wait 200
		accum 0 set 5
	}

	trigger p4talk1
	{
		wait 250
		trigger partisan4 talk1
	}

	trigger p4door3
	{
		wait 2000
		alertentity openp4door3
	}

	trigger p4door4
	{
		wait 2000
		alertentity openp4door2
	}
}

slidefloor
{
	spawn
	{
	}

	trigger open
	{
		playsound sound/world/winerack.wav loop
		gotomarker floorspot2 30
		stopsound
	}
}