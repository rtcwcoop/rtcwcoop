//
//Brush Scripting
//Map: Escape #1
//Last edited by: Dan
//

dialog // dialog counter
{
	spawn
	{
		accum 0 bitset 0
		accum 0 bitset 1
		accum 0 bitset 2
		accum 0 bitset 3
	}

	trigger timeing
	{
		accum 0 abort_if_not_bitset 0
		wait 1200
		trigger nazi1 door1
		wait 7500
		trigger nazi1 answer1
		wait 6000
		trigger nazi1 answer2
	}

	trigger killdialog1
	{
		accum 0 abort_if_not_bitset 0
		accum 0 bitreset 0
		accum 0 bitreset 1
		resetscript
	}

	trigger radiotune
	{
		accum 0 abort_if_not_bitset 1
		trigger nazi2 reply1a
		trigger nazi3 talk3
		wait 3000
		trigger nazi3 weather
		wait 6100
		trigger nazi3 thanks
	}

	trigger killdialog2
	{
		accum 0 abort_if_not_bitset 1
		accum 0 bitreset 0
		accum 0 bitreset 1
		resetscript
	}

	trigger anynews
	{
		accum 0 abort_if_not_bitset 2
		wait 900
		trigger nazi5 reply5a
		wait 1800
		trigger nazi2 reply2a
		wait 3700
		trigger nazi5 reply5b
		wait 3500
//		trigger nazi2 reply2b
		wait 3000
		trigger nazi5 reply5c
		wait 2800
		trigger nazi2 reply2c
	}

	trigger killdialog3
	{
		accum 0 abort_if_not_bitset 2
		accum 0 bitreset 2
		resetscript
	}

	trigger radiotalk
	{
		accum 0 abort_if_not_bitset 3
		trigger nazi12 radio12a
		wait 2800
		trigger nazi13 radio13a
		wait 2250
		trigger nazi12 radio12b
		wait 4500
		trigger nazi13 radio13b
		wait 2700
		trigger nazi12 radio12c
		wait 2700
		trigger nazi13 radio13c
		wait 3600
		trigger nazi12 radio12d
		wait 3800
		trigger nazi12 towindow
		wait 200
		trigger nazi13 toradio
	}

	trigger killdialog4
	{
		accum 0 abort_if_not_bitset 3
		accum 0 bitreset 3
		resetscript
	}

}

spawner //escape1 spawner
{
	spawn
	{
		accum 0 bitset 0 // zone1 spawn
		accum 0 bitset 1 // radio room spawn
		accum 0 bitset 2 // barracks room spawn
		accum 0 bitset 3 // meeting room spawn
		accum 0 bitset 4 // dlight 1 toggle
		accum 0 bitset 5 // dlight 2 toggle
		accum 1 set 0 // alarm 0=off 1=on
		accum 2 set 0 // boardxtra counter
	}

	trigger zone1calm
	{
		accum 0 abort_if_not_bitset 0
		accum 0 bitreset 0

		trigger nazi2 getgoing2
		alertentity nazi3
		alertentity nazi5
	}

	trigger zone1alert
	{
		accum 0 abort_if_not_bitset 0
		accum 0 bitreset 0

		trigger nazi2 bringiton2
		alertentity nazi3
		alertentity nazi5
	}

	trigger radioroom
	{
		accum 0 abort_if_not_bitset 1
		accum 0 bitreset 1

		alertentity nazi12
		alertentity nazi13
	}

	trigger barracks
	{
		accum 0 abort_if_not_bitset 2
		accum 0 bitreset 2

		alertentity nazi10
	}

	trigger meetingroom
	{
		accum 0 abort_if_not_bitset 3
		accum 0 bitreset 3
		alertentity nazi14
		wait 100
		accum 1 abort_if_not_equal 1
		trigger nazi14 alarm14
	}

	trigger boardxtra
	{
		accum 2 inc 1
		accum 2 abort_if_not_equal 2
		accum 2 set 10
		alertentity nazi24
		alertentity nazi25
	}

	trigger alarmset
	{
		accum 1 set 1
	}

	trigger dlight1off
	{
		accum 0 abort_if_bitset 4
		alertentity cine1_dlight1
		accum 0 bitset 4
	}

	trigger dlight1on
	{
		accum 0 abort_if_not_bitset 4
		alertentity cine1_dlight1
		accum 0 bitreset 4
	}

	trigger dlight2off
	{
		accum 0 abort_if_bitset 5
		alertentity cine1_elect2
		accum 0 bitset 5
	}

	trigger dlight2on
	{
		accum 0 abort_if_not_bitset 5
		alertentity cine1_elect2
		accum 0 bitreset 5
	}

}

alarm //alarm counter
{
	spawn
	{
		accum 0 bitset 0
		accum 0 bitset 1 // call alarmspawn once
		accum 0 bitset 2 // call gethelp once
		accum 1 set 0 // alarm 0=off 1=on
	}

	trigger gethelp
	{
		accum 0 abort_if_not_bitset 2
		accum 0 bitreset 2
		wait 2000
		accum 0 abort_if_not_bitset 0
		alertentity nazi4
	}

	trigger exitcells
	{
		accum 0 bitreset 0
	}

	trigger alarmspawn // alarm called for by others
	{
		accum 0 abort_if_not_bitset 1
		accum 0 bitreset 1

		trigger spawner radioroom
		trigger spawner barracks
		trigger nazi12 pushalarm
	}

	trigger alarmspawn2 // alarm set by nazi12
	{
		accum 0 abort_if_not_bitset 1
		accum 0 bitreset 1

		trigger spawner radioroom
		trigger spawner barracks
	}

	trigger alarmon
	{
		accum 1 abort_if_not_equal 0
		accum 1 set 1 // alarm 0=off 1=on

		trigger player musicmoving
		trigger nazi13 alarm13
		trigger nazi7 alarm7
		trigger nazi8 alarm8
		trigger nazi6 alarm6
		trigger nazi9 alarm9
		trigger spawner alarmset
		trigger nazi10 alarm10
		trigger nazi12 playeralarm
	}
}

dummy
{
	spawn
	{
		accum 0 bitset 0 // bitset = off  bitreset = on
		accum 0 bitset 1
		accum 1 set 0
	}

	// ---Start Cinematic1---

	trigger cinematic1
	{
		wait 3000
		playsound sound/movers/switches/butn2.wav
		alertentity cine1_elect1a
		trigger spawner dlight1on
		wait 250
		alertentity cine1_scream1
		wait 3750
		trigger spawner dlight1off
		wait 2000
		playsound sound/movers/switches/butn2.wav
		alertentity cine1_elect1a
		trigger spawner dlight1on
		wait 250
		alertentity cine1_scream1
		wait 3750
		trigger spawner dlight1off
		wait 2000
		trigger cine1_drz talk1		// Come in, Sergeant, Come in.
		wait 2000
		trigger cine1_drz anim1
		trigger player cam2
		trigger cine1_nazi1 go1
		wait 7500
		trigger cine1_drz anim1
		trigger player cam3
		wait 2500
		trigger cine1_nazi1 talk1	// Orders, Herr Doctor?
		wait 3000
		trigger cine1_drz talk2		// Go and get zee other one...
		trigger player cam4
		wait 3000
		trigger cine1_nazi1 talk2	// Jawol!
		wait 1500
		trigger player cam5
		trigger cine1_nazi1 go2
		wait 5250
		trigger cine1_drz talk3		// Now, let's try again, shall we? Who do you work for?
		trigger player cam7
		wait 4750
		trigger dummy cinematic1a
	}

	trigger cinematic1a
	{
		alertentity cine1_elect
		trigger spawner dlight2on
		alertentity cine1_nazi2
		wait 250
		alertentity cine1_scream2
		alertentity kill_c_nazi1
		trigger player cam8
		wait 3750
		trigger spawner dlight2off
		wait 4500
		alertentity cine1_nazi3
		wait 250
		alertentity kill_c_nazi2
		wait 250
		trigger player cam9
//
		wait 8500
		trigger player cam11
		wait 200
		trigger cine_door camera_up
		wait 500
		trigger cine1_nazi3 go2
//
		wait 6000
		trigger cine1_bj anim1
		wait 500
		trigger cine1_bj cine_snd
		wait 100
		trigger player cam11d
		wait 3000
		trigger dummy cine1_cleanup
	}

	trigger cine1_cleanup
	{
		trigger cine_door fast_up
		alertentity kill_c_nazi1
		alertentity kill_c_nazi2
		alertentity kill_cine1
		wait 5
		alertentity nazi1
		wait 5
		alertentity nazi2
		alertentity spawngun
		trigger player fix_bitset0
		trigger spawner dlight1off
		trigger spawner dlight2off
	}

	// ---End Cinematic1---

	trigger smoke
	{
		alertentity smoke1
		wait 13000
		alertentity smoke1
	}

	trigger surgicalon
	{
		accum 0 abort_if_not_bitset 0
		alertentity water1
		accum 0 bitreset 0
	}

	trigger surgicaloff
	{
		accum 0 abort_if_bitset 0
		alertentity water1
		accum 0 bitset 0
	}

	trigger moveback
	{
		accum 0 abort_if_bitset 1
		accum 0 bitreset 1
		wait 4000
		trigger nazi17 return17
		trigger nazi18 return18
	}

	trigger finalbunch
	{
		accum 1 inc 1
		accum 1 abort_if_less_than 5
		trigger player musicmission
	}
}

handle
{
	spawn
	{

	}

	trigger turnon
	{
		playsound sound/movers/switches/butn2.wav
		alertentity start_tesla
		faceangles 180 0 0 500
		alertentity hurt1
		alertentity smoke1
		wait 3000
		alertentity hurt1
		wait 1000
		alertentity smoke1
		faceangles 0 0 0 500
	}
}

cine_door
{
	spawn
	{
	
	}

	trigger camera_up
	{
		wait 5
		playsound sound/movers/doors/door6_loopo.wav
		gotomarker doorspot2 55
		stopsound
	}

	trigger fast_up
	{
		stopsound
		resetscript
		gotomarker doorspot2 2000
	}
}